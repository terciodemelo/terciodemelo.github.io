<html><head><link href="index.css?859.5161832181695" rel="stylesheet" /><title>Tercio de Melo</title></head><body><body><div class="header"><h1>Tercio de Melo</h1><nav><a class="current-page" href="/index.html">HOME</a><a href="/about.html">ABOUT</a><a href="/posts.html">POSTS</a><a href="/tags.html">TAGS</a></nav></div><h2>New website!</h2><p>New website is coming along!!</p><p>We now even have multiple paragraphs...</p><h2>No JavaScript</h2><p>This website is made without any javascript.</p><p>Though the website itself is HTML and CSS only, it is generated from clojure code. I have previously used <a href="https://gohugo.io/">Hugo</a> to generate static website, but it is too cumbersome. I wanted something simple, but with powerful features such as hot reloading and custom logic for building the pages.</p><h2>Clojure for the rescue</h2><p>Of course a <a href="https://pt.wikipedia.org/wiki/Lisp">Lisp</a> would be the best tool for the job, code and data is the same, for being a homoiconic language; and <a href="https://clojure.org/">Clojure</a> is currently my flavor of Lisp.</p><p>Below is a snippet of the clojure code that for now generates this <code>index.html</code> file:</p><pre>(ns main
  (:gen-class)
  (:use [hiccup.core])
  (:require [clojure.java.io :as io]
            [clojure.core.async :refer [go]]
            [juxt.dirwatch :refer [watch-dir]]
            [clojure.string :as string]
            [dev.server]
            [components.components :as components]))

(defn head [should-refresh]
  [:head
   [:link {:rel "stylesheet" :href (str "index.css?" (rand 1000))}]
   [:title "Tercio de Melo"]
   (if should-refresh
     [:meta {:http-equiv "refresh"  :content "2"}])])

(defn body [page]
  (let [module  (symbol (str "pages." page))
        content (symbol (str module "/content"))]
    (require module)
    [:body
     (components/header page)
     ((resolve content))]))

(defn footer []
  [:footer
   [:span "2018-2021"]])

(defn pages [base-dir]
  (let [directory (io/file (str base-dir "/src/pages"))]
    (->> (.list directory)
         (filter #(string/ends-with? % ".clj"))
         (map #(string/replace % #"\.clj$" ""))
         vec)))

(defn -main [& args]
  (doseq [page (pages (System/getProperty "user.dir"))]
    (let [output-file (str "tercio.com.br/" page ".html")
          sysargs     (set args)
          is-dev-env  (contains? sysargs "--environment=dev")]
      (println "Generating " output-file)
      (with-open [wtr (io/writer output-file)]
        (.write wtr (html [:html (head is-dev-env)
                           [:body (body page)]
                           (footer)]))))))

(defn watch-in-cider []
  (let [class-path (System/getProperty "java.class.path")]
    (if (.contains class-path "/cider-nrepl/")
      (do
        (println "Starting dev server on port 8000")
        (go (dev.server/start 8000))

        (println "Watching page changes")
        (watch-dir
         (fn [& _] (-main "--environment=dev"))
         (io/file "src/pages")
         (io/file "src/components")
         (io/file "src/posts"))))))

(comment (watch-in-cider))
(comment (-main))
(comment (-main "--environment=dev"))
(comment (dev.server/start 8000))
</pre><p>The <code>&lt;meta ...&gt;</code> tag triggers refresh every too second. And every time I save a file, say <code>src/pages/index.clj</code>, in <a href="https://www.gnu.org/software/emacs/">Emacs</a> then <a href="https://github.com/clojure-emacs/cider">CIDER</a> automatically creates a new <code>index.html</code>. Notice that I'm using <a href="https://github.com/juxt/dirwatch">juxt.dirwatch</a> to watch page changes locally. This watch is executed only when CIDER is in the class path, which doesn't happen when we run it with a <code>lein run</code>, for example.</p><p>Who already had some contact with <a href="https://clojurescript.org/">ClojureScript</a> might have noticed that I leverage <a href="https://github.com/weavejester/hiccup">Hiccup</a> to generate HTML from Clojure data structures. It is a really fun and powerful tool.</p><p>If you are interested in seing the details, you will find the code in <a href="https://github.com/terciodemelo/terciodemelo.github.io">GitHub</a>. This website is hosted in <a href="https://pages.github.com/">GitHub Pages</a>.</p></body></body><footer><span>2018-2021</span></footer></html>